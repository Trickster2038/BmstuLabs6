;********************************************************************
;Программа 4.1 для МК ATx8515:
;демонстрация работы таймера Т0 в режиме счётчика событий;
;событие - нажатие кнопки SW0.
;Соединения: порт PB0–SW0, шлейфом порт PD-LED
;Светодиоды включаются после четвертого нажатия кнопки SW0
;********************************************************************
;.include "8515def.inc" ;файл определений AT90S8515
.include "m8515def.inc" ;файл определений ATmega8515
.def temp = r16 ;временный регистр
;***Таблица векторов прерываний
.org $000
rjmp INIT ;обработка сброса
.org $007
rjmp T0_OVF ;обработка переполнения таймера T0
;***Инициализация МК

INIT: ldi temp,low(RAMEND) ;установка
out SPL,temp ; указателя стека
ldi temp,high(RAMEND) ; на последнюю
out SPH,temp ; ячейку ОЗУ
clr temp ;инициализация выводов порта PB
out DDRB,temp ; на ввод
ldi temp,(1<<PB0) ;включение ‘подтягивающего’ резистора
out PORTB,temp ; входа PB0
ser temp ;инициализация выводов порта PD
out DDRD,temp ; на вывод
out PORTD,temp ;выключение светодиодов
ldi temp,(1<<SE) ;разрешение перехода
out MCUCR,temp ; в режим Idle
;***Настройка таймера Т0 на режим счётчика событий
ldi temp,0x02 ;разрешение прерывания по
out TIMSK,temp ; переполнению таймера Т0
ldi temp,0x07 ;переключение таймера
out TCCR0,temp ; по положительному перепаду напряжения
sei ;глобальное разрешение прерываний
ldi temp,0xFC ;$FC=-4 для
out TCNT0,temp ; отсчёта 4-х нажатий


LOOP: sleep ;переход в режим пониженного
nop ; энергопотребления
rjmp LOOP

;***Обработка прерывания при переполнении таймера T0
T0_OVF: clr temp
out PORTD,temp ;включение светодиодов
rcall DELAY ;задержка
ser temp
out PORTD,temp ;выключение светодиодов
ldi temp,0xFC ;перезагрузка
out TCNT0,temp ; TCNT0
reti

;*** Задержка ***
DELAY: ldi r19,6
ldi r20,255
ldi r21,255

dd: dec r21
brne dd
dec r20
brne dd
dec r19
brne dd
ret
